#!/usr/local/bin/perl -w

use strict;
use Carp;
use FileHandle;

##########################################################
##  VECTOR1
##
##  Usage:   vector1     (no command line arguments)
##
##  The function &main_loop below gives the menu for the system.
##
##  This is an example program that shows how the core
##  of a vector-based IR engine may be implemented in Perl.
##
##  Some of the functions below are unimplemented, and some
##  are only partially implemented. Suggestions for additions
##  are given below and in the assignment handout.
##
##  You should feel free to modify this program directly,
##  and probably use this as a base for your implemented
##  extensions.  As with all assignments, the range of
##  possible enhancements is open ended and creativity
##  is strongly encouraged.
##########################################################


############################################################
## Program Defaults and Global Variables
############################################################

##my $DIR  = "/home/1/yarowsky/cs466/hw2";
# changed directory so that it can run on my local machine
my $DIR = ".";
my $HOME = ".";

my $token_docs = "$DIR/myfile";           # tokenized cacm journals
my $corps_freq = "$DIR/myfile";           # frequency of each token in the journ.
my $stoplist   = "$DIR/common_words";   # common uninteresting words
my $crypto_words = "$DIR/key_words";        # words most relevant in the field of cryptocurrency


# @doc_vector
#
#   An array of hashes, each array index indicating a particular document's
#   weight "vector".

my @doc_vector = ( );


# %docs_freq_hash
#
# associative array which holds <token, frequency> pairs where
#
#   token     = a particular word or tag found in the cacm corpus
#   frequency = the total number of times the token appears in
#               the corpus.

my %docs_freq_hash = ( );

# %corp_freq_hash
#
# associative array which holds <token, frequency> pairs where
#
#   token     = a particular word or tag found in the corpus
#   frequency = the total number of times the token appears per
#               document-- that is a token is counted only once
#               per document if it is present (even if it appears
#               several times within that document).

my %corp_freq_hash = ( );

# %stoplist_hash
#
# common list of uninteresting words which are likely irrelvant
# to any query.
#
#   Note: this is an associative array to provide fast lookups
#         of these boring words

my %stoplist_hash  = ( );


# %relevance_hash
#
# a hash of hashes where each <key, value> pair consists of
#
#   key   = a query number
#   value = a hash consisting of document number keys with associated
#           numeric values indicating the degree of relevance the
#           document has to the particular query.

my %relevance_hash = ( );

# @crypto_hash
#
# a hash used for storing words most relevant in cryptocurrency
# and thus should be given a greater weight

my %crypto_hash = ( );

# @doc_simula
#
# array used for storing query to document or document to document
# similarity calculations (determined by cosine_similarity, etc. )

my @doc_simula = ( );

# @res_vector
#
# array used for storing the document numbers of the most relevant
# documents in a query to document or document to document calculation.

my @res_vector = ( );



# start program

&main_loop;

##########################################################
##  INIT_FILES
##
##  This function specifies the names and locations of
##  input files used by the program.
##
##  Parameter:  $type   ("stemmed" or "unstemmed")
##
##  If $type == "stemmed", the filenames are initialized
##  to the versions stemmed with the Porter stemmer, while
##  in the default ("unstemmed") case initializes to files
##  containing raw, unstemmed tokens.
##########################################################

sub init_files {

    if ("stemmed" eq (shift || "")) {

	$token_docs .= "\.stemmed";
	$corps_freq .= "\.stemmed\.hist";
	$stoplist   .= "\.stemmed";

    }
    else {

	$token_docs .= "\.tokenized";
	$corps_freq .= "\.tokenized\.hist";


    }
}

##########################################################
##  INIT_CORP_FREQ
##
##  This function reads in corpus and document frequencies from
##  the provided histogram file for both the document set
##  and the query set. This information will be used in
##  term weighting.
##
##  It also initializes the arrays representing the stoplist,
##  title list and relevance of document given query.
##########################################################

sub init_corp_freq {

    my $corps_freq_fh = new FileHandle $corps_freq, "r"
	or croak "Failed $corps_freq";

    my $stoplist_fh   = new FileHandle $stoplist  , "r"
	or croak "Failed $stoplist";

    my $crypto_fh = new FileHandle $crypto_words  , "r"
    or croak "Failed $crypto_words";

    my $line = undef;

    while (defined( $line = <$corps_freq_fh> )) {

	# so on my computer split will return a first element of undef
	# if the leading characters are white space, so I eat the white
	# space to insure that the split works right.

	my ($str) = ($line =~ /^\s*(\S.*)/);

	my ($doc_freq,
	    $cor_freq,
	    $term    ) = split /\s+/, $str;

	$docs_freq_hash{ $term } = $doc_freq;
	$corp_freq_hash{ $term } = $cor_freq;
    }

    while (defined( $line = <$stoplist_fh> )) {

	chomp $line;
	$stoplist_hash{ $line } = 1;
    }

    while (defined( $line = <$crypto_fh> )) {

    chomp $line;
    $crypto_hash{ $line } = 1;
    }

}


##########################################################
##  INIT_DOC_VECTORS
##
##  This function reads in tokens from the document file.
##  When a .I token is encountered, indicating a document
##  break, a new vector is begun. When individual terms
##  are encountered, they are added to a running sum of
##  term frequencies. To save time and space, it is possible
##  to normalize these term frequencies by inverse document
##  frequency (or whatever other weighting strategy is
##  being used) while the terms are being summed or in
##  a posthoc pass.  The 2D vector array
##
##    $doc_vector[ $doc_num ]{ $term }
##
##  stores these normalized term weights.
##
##  It is possible to weight different regions of the document
##  differently depending on likely importance to the classification.
##  The relative base weighting factors can be set when
##  different segment boundaries are encountered.
##
##  This function is currently set up for simple TF weighting.
##########################################################

sub init_doc_vectors {
    my $token_docs_fh = new FileHandle $token_docs, "r"
	or croak "Failed $token_docs";

    my $word    = undef;

    my $doc_num =  0;    # current document number and total docs at end
    my $tweight =  0;    # current weight assigned to document token

    push @doc_vector, { };     # push one empty value onto @doc_vector so that
                               # indices correspond with document numbers

    while (defined( $word = <$token_docs_fh> )) {

	chomp $word;

	last if $word =~ /^\.I 0$/; # indicates end of file so kick out

	if ($word =~ /^\.I/) {     # indicates start of a new document
      print $word;
	    push @doc_vector, { };
	    $doc_num++;

	    next;
	}


	# not implemented yet
	if ($word =~ /[a-zA-Z]/ and ! exists $stoplist_hash{ lc($word) }) {

  #    print $word, "\n";
  #    print $docs_freq_hash{ $word }, "\n";

	    if (defined( $docs_freq_hash{ $word } ) ) {
          #  if (exists $crypto_hash{ lc ($word) }) {
           #         $doc_vector[$doc_num]{ $word } += 10;
           # } else {
				$doc_vector[$doc_num]{ $word } += 1; # Term Frequency: # occurences of term t in document d
  #      print $doc_vector[$doc_num]{ $word }, "\n";
            #}
	    }
	    else {
		print "ERROR: Document frequency of zero: ", $word, "\n";
	    }
	}
    }

    # optionally n(ormalize the raw term frequency
        # TF IDF weighting (wt_{t,d} = TF_{td} * log(N/DF)))

    	foreach my $hash (@doc_vector) {
    	  	foreach my $key (keys %{ $hash }) {
               
    	         #  $hash->{ $key } = $hash->{ $key } * log( $doc_num / $docs_freq_hash{ $key });
                
                if (exists $crypto_hash{ lc ($key) }) {
                    $hash->{ $key } = $hash->{ $key } + 3;
             #       print($key, "->", $hash->{ $key }, "\n");
                } 
                
             #       $hash->{ $key } = 1;
           #  print($key, "->", $hash->{ $key }, "\n");
            }

                 
    	}
            
   		


    return $doc_num;
}


##########################################################
## MAIN_LOOP
##
## Parameters: currently no explicit parameters.
##             performance dictated by user imput.
##
## Initializes document and query vectors using the
## input files specified in &init_files. Then offers
## a menu and switch to appropriate functions in an
## endless loop.
##
## Possible extensions at this level:  prompt the user
## to specify additional system parameters, such as the
## similarity function to be used.
##
## Currently, the key parameters to the system (stemmed/unstemmed,
## stoplist/no-stoplist, term weighting functions, vector
## similarity functions) are hardwired in.
##
## Initializing the document vectors is clearly the
## most time consuming section of the program, as 213334
## to 258429 tokens must be processed, weighted and added
## to dynamically growing vectors.
##
##########################################################

sub main_loop {
    print "INITIALIZING VECTORS ... \n";

  &init_files ("unstemmed");

	&init_corp_freq;

  my $total_docs = &init_doc_vectors();


  while (1) {

	print <<"EndOfMenu";

	============================================================
	==     Welcome to the 600.466 Cryptocurrency Analysis Engine
	==
        == Total Documents: $total_docs
	============================================================

	OPTIONS:
	  1 = Find the similarity between two cryptocurrencies
	  2 = Find cryptocurrencies most similar to a given one
	  3 = Find the best cryptocurrency for each clustering
	  4 = Quit

	============================================================

EndOfMenu
    ;

	print "Enter Option: ";

	my    $option = <STDIN>;
	chomp $option;

	exit 0 if $option == 4;

	&do_full_cosine_similarity and next if $option == 1;
    &get_and_show_retrieved_set and next if $option == 2;
#  &find_competitors and next if $option == 2;
#	&find_best and next if $option == 3;

    }
}

##########################################################
## GET_AND_SHOW_RETRIEVED_SET
##   
##  This function requests key retrieval parameters,
##  including:
##  
##  A) Is a query vector or document vector being used
##     as the retrieval seed? Both are vector representations
##     but they are stored in different data structures,
##     and one may optionally want to treat them slightly
##     differently.
##
##  B) Enter the number of the query or document vector to
##     be used as the retrieval seed.
##
##     Alternately, one may wish to request a new query
##     from standard input here (and call the appropriate
##     tokenization, stemming and term-weighting routines).
##
##  C) Request the maximum number of retrieved documents
##     to display.
##
##  Perl note: one reads a line from a file <FILE> or <STDIN>
##             by the assignment $string=<STDIN>; Beware of
##             string equality testing, as these strings 
##             will have a newline (\n) attached.
##########################################################

sub get_and_show_retrieved_set {

    # if not an interactive query than we need to retrieve which
    # query/document we want to use from the corpus
    
    my $vect_num = 1;

    print "Target Document/Query number: ";

          $vect_num  = <STDIN>;
    chomp $vect_num;

    if   ($vect_num !~ /^[1-9]/) { $vect_num  = 1; }

    print "\n";
    


    print "Show how many matching documents (20): ";
    
    my    $max_show  = <STDIN>;
    chomp $max_show;

    if   ($max_show !~ /[0-9]/) { $max_show  = 20; }

    

    print "Document to Document comparison\n";
    
    &get_retrieved_set( $doc_vector[$vect_num]);
    &shw_retrieved_set( $max_show, 
                $vect_num, 
                $doc_vector[$vect_num]);
    
}

###########################################################
## GET_RETRIEVED_SET
##
##  Parameters:
## 
##  $qry_vector{} - the query vector to be compared with the
##                  document set. May also be another document 
##                  vector.
##
##  This function computes the document similarity between the
##  given vector $qry_vector{} and all vectors in the document
##  collection storing these values in the array @doc_simula
##
##  An array of the document numbers is then sorted by this
##  similarity function, forming the rank order of documents
##  for use in the retrieval set.  
##
##  The -1 in the simcomp similarity comparision function
##  makes the sorted list in descending order.
##########################################################
 
sub get_retrieved_set {

    my $qry_vector = shift;
    my $method  = shift;
    my $tot_number = (scalar @doc_vector) - 1;
    my $index      = 0;

    @doc_simula = ( );   # insure that storage vectors are empty before we
    @res_vector = ( );   # calculate vector similarities

    push @doc_simula, 0.0;    # push one empty value so that indices 
                              # correspond with document values

    for $index ( 1 .. $tot_number) {
        
            push @doc_simula, &cosine_sim_a( $qry_vector, $doc_vector[$index], $index);
        
    }


    @res_vector = 
      sort { -1 * ($doc_simula[$a] <=> $doc_simula[$b]); } 1 .. $tot_number;
}

############################################################
## SHW_RETRIEVED_SET
##
## Assumes the following global data structures have been
## initialized, based on the results of &get_retrieved_set.
##
## 1) @res_vector - contains the document numbers sorted in 
##                  rank order
## 2) @doc_simula - The similarity measure for each document, 
##                  computed by &get_retrieved_set.
##
## Also assumes that the following have been initialized in
## advance:
##
##       $titles[ $doc_num ]    - the document title for a 
##                                document number, $doc_num
##       $relevance_hash{ $qry_num }{ $doc_num }
##                              - is $doc_num relevant given
##                                query number, $qry_num
##
## Parameters:
##   $max_show   - the maximum number of matched documents 
##                 to display.
##   $qry_num    - the vector number of the query
##   $qry_vect   - the query vector (passed by reference)
##   $comparison - "Query" or "Document" (type of vector 
##                 being compared to)
##
## In the case of "Query"-based retrieval, the relevance 
## judgements for the returned set are displayed. This is 
## ignored when doing document-to-document comparisons, as 
## there are nor relevance judgements.
##
############################################################

sub shw_retrieved_set {

    my $max_show   = shift;
    my $qry_num    = shift;
    my $qry_vect   = shift;


    print << "EndOfList";

    ************************************************************
    Documents Most Similar To Document number $qry_num
    ************************************************************
    Similarity   Doc#  
    ==========   ==== 

EndOfList
    ;

    my $rel_num = $qry_num;
    my $index   = 0;

    for $index ( 0 .. $max_show ) {
    my $ind = $res_vector[$index];
    my $id = $ind;
    print "  ";
    

    my ($similarity) = $doc_simula[$ind] == 1 ? "1.00000000" : ($doc_simula[$ind]    =~ /^([0-9]+\.\d{0,8})/);
   # my  $title       = substr $titles_vector[$ind], 0, 47;

   
    printf("   %8f     %d\n", $similarity, $ind);
    #print($id, "\n");

    }

    print "\n";
    print "Show the terms that overlap between the query and ";
    print "retrieved docs (y/n): ";

    my  $show_terms = <STDIN>;
    if ($show_terms !~ /[nN]/) {

    my $index = 0;

    for $index ( 0 .. $max_show ) {
        my $ind = $res_vector[$index];

        show_overlap( $qry_vect,
              $doc_vector[$ind],
              $qry_num,
              $ind );

        if ($index % 5 == 4) {

        print "\n";
        print "Continue (y/n)? ";

        my  $cont = <STDIN>;
        if ($cont =~ /[nN]/) {
            last;
        }
        }
    }
    }
}

########################################################
## SHOW_OVERLAP
## 
## Parameters:
##  - Two vectors ($qry_vect and $doc_vect), passed by
##    reference.
##  - The number of the vectors for display purposes
##
## PARTIALLY IMPLEMENTED:
## 
## This function should show the terms that two vectors
## have in common, the relative weights of these terms
## in the two vectors, and any additional useful information
## such as the document frequency of the terms, etc.
##
## Useful for understanding the reason why documents
## are judged as relevant. 
##
## Present in a sorted order most informative to the user.
##
########################################################

sub show_overlap {

    my $qry_vect = shift;
    my $doc_vect = shift;
    my $qry_num  = shift;
    my $doc_num  = shift;

    print "============================================================\n";
    printf( "%-15s  %8d   %8d\t%s\n", 
       "Vector Overlap",
       $qry_num        ,
       $doc_num        ,
       "Docfreq"       );
    print "============================================================\n";

    my $term_one   = undef;
    my $weight_one = undef;

    while (($term_one, $weight_one) = each %{ $qry_vect }) {
    if (exists $$doc_vect{ $term_one }) {

        printf( "%-15s  %8d   %8d\t%d\n"    ,
           $term_one                    ,
           $weight_one                  ,
           $$doc_vect{ $term_one }      ,
           $docs_freq_hash{ $term_one } );
    }
    }
}

########################################################
## DO_FULL_COSINE_SIMILARITY
##
##  Prompts for a document number and query number,
##  and then calls a function to show similarity.
##
##  Could/should be expanded to handle a variety of
##  similarity measures.
########################################################

sub do_full_cosine_similarity {

    print "\n";
    print "1st Document number: ";

    my    $num_one = <STDIN>;
    chomp $num_one;

    print "\n";
    print "2nd Document number: ";

    my    $num_two = <STDIN>;
    chomp $num_two;

    $num_one = 1 if $num_one !~ /[0-9]/;
    $num_two = 1 if $num_two !~ /[0-9]/;

    full_cosine_similarity( $doc_vector[$num_one],
			    $doc_vector[$num_two],
			    $num_one,
			    $num_two );
}


########################################################
## FULL_COSINE_SIMILARITY
##
## UNIMPLEMENTED
##
## This function should compute cosine similarity between
## two vectors and display the information that went into
## this calculation, useful for debugging purposes.
## Similar in structure to &show_overlap.
########################################################

sub full_cosine_similarity {

    my $qry_vect = shift;
    my $doc_vect = shift;
    my $qry_indx = shift;
    my $doc_indx = shift;
    my $term_one   = undef;
    my $weight_one = undef;
    my $term_two   = undef;
    my $weight_two = undef;


    print "============================================================\n";
    printf( "%-15s  %8d   %8d\t%-15s\n",
	   "Vector Overlap",
	   $qry_indx        ,
	   $doc_indx        ,
	   "Product result"       );
    print "============================================================\n";


    my $num = 0;
    my $sum1 = 0;
    my $sum2 = 0;
    my $sum_square1 = 0;
    my $sum_square2 = 0;

    while (($term_one, $weight_one) = each %{ $qry_vect }) {
    	$num += $weight_one * ($$doc_vect{ $term_one } || 0);
    	$sum1 += $weight_one;
    	$sum_square1 += ($weight_one * $weight_one);

    	if (exists $$doc_vect{ $term_one }) {
    		printf( "%-15s  %8d   %8d\t%8d\n",
    		$term_one                    ,
		   	$weight_one                  ,
		   	$$doc_vect{ $term_one }      ,
		   	$weight_one * $$doc_vect{ $term_one } );
    	}
    }

    while (($term_two, $weight_two) = each %{ $doc_vect }) {
    	$sum2 += $weight_two;
    	$sum_square2 += ($weight_two * $weight_two);
    }


    my $cos_sim = &cosine_sim_b($num, $sum_square1, $sum_square2);

    print "==================================\n";
    printf( "%-15s\t\n", "Cosine Similarity");
	printf( "%8f\t\n", $cos_sim);
    print "==================================\n";
}


########################################################
## COSINE_SIM_A
##
## Computes the cosine similarity for two vectors
## represented as associate arrays.
########################################################

sub cosine_sim_a {

    my $vec1 = shift;
    my $vec2 = shift;
    my $index = shift;

    my $num     = 0;
    my $sum_sq1 = 0;
    my $sum_sq2 = 0;

    my @val1 = values %{ $vec1 };
    my @val2 = values %{ $vec2 };

    # determine shortest length vector. This should speed
    # things up if one vector is considerable longer than
    # the other (i.e. query vector to document vector).

    if ((scalar @val1) > (scalar @val2)) {
	my $tmp  = $vec1;
	   $vec1 = $vec2;
	   $vec2 = $tmp;
    }

    # calculate the cross product

    my $key = undef;
    my $val = undef;

    while (($key, $val) = each %{ $vec1 }) {
	$num += $val * ($$vec2{ $key } || 0);
    }

    # calculate the sum of squares

    my $term = undef;

    foreach $term (@val1) { $sum_sq1 += $term * $term; }
    #print("==========Got Here==========\n");
    foreach $term (@val2) { $sum_sq2 += $term * $term; }

    if ($sum_sq2 == 0) {
        print("Doc ", $index, " has sum_sq2 = 0\n ");
    }
    return ( $num / sqrt( $sum_sq1 * $sum_sq2 ));
}


########################################################
##  COSINE_SIM_B
##
##  This function assumes that the sum of the squares
##  of the term weights have been stored in advance for
##  each document and are passed as arguments.
########################################################

sub cosine_sim_b {

    my $value = shift;
    my $sum_sq1 = shift;
    my $sum_sq2 = shift;

    return ( $value / sqrt( $sum_sq1 * $sum_sq2 ));
}
################################
## JACCARD_SIM_A
##
## Computes the cosine similarity for two vectors
## represented as associate arrays.
##
################################
sub jaccard_sim_a {

    my $vec1 = shift;
    my $vec2 = shift;

    my $num     = 0;
    my $sum_term_1 = 0;
    my $sum_term_2 = 0;

    my @val1 = values %{ $vec1 };
    my @val2 = values %{ $vec2 };

    # determine shortest length vector. This should speed
    # things up if one vector is considerable longer than
    # the other (i.e. query vector to document vector).

    if ((scalar @val1) > (scalar @val2)) {
	my $tmp  = $vec1;
	   $vec1 = $vec2;
	   $vec2 = $tmp;
    }

    # calculate the cross product

    my $key = undef;
    my $val = undef;

    while (($key, $val) = each %{ $vec1 }) {
	$num += $val * ($$vec2{ $key } || 0);
    }

    # calculate the sum of squares

    my $term = undef;

    foreach $term (@val1) { $sum_term_1 += $term; }
    foreach $term (@val2) { $sum_term_2 += $term; }

    return ( $num / (($sum_term_1 + $sum_term_2 ) - $num));

}

######################################################
##  JACCARD_SIM_B
##  This function assumes that the sum
##  of the term weights have been stored in advance for
##  each document and are passed as arguments.
##
######################################################

sub jaccard_sim_b {

    my $num  = shift;
    my $sum_term1 = shift;
    my $sum_term2 = shift;

    return ( $num / ($sum_term1 + $sum_term2 - $num) );
}

######################################################
##  DICE_SIM_A
##
##  Computes the dice similarity for two vectors
##  represented as associate arrays
##
######################################################

sub dice_sim_a {


    my $vec1 = shift;
    my $vec2 = shift;

    my $num     = 0;
    my $sum_term_1 = 0;
    my $sum_term_2 = 0;

    my @val1 = values %{ $vec1 };
    my @val2 = values %{ $vec2 };

    # determine shortest length vector. This should speed
    # things up if one vector is considerable longer than
    # the other (i.e. query vector to document vector).

    if ((scalar @val1) > (scalar @val2)) {
	my $tmp  = $vec1;
	   $vec1 = $vec2;
	   $vec2 = $tmp;
    }

    # calculate the cross product

    my $key = undef;
    my $val = undef;

    while (($key, $val) = each %{ $vec1 }) {
	$num += $val * ($$vec2{ $key } || 0);
    }

    # calculate the sum of squares

    my $term = undef;

    # 2 * numerator / (sum_term + sum_term)
    foreach $term (@val1) { $sum_term_1 += $term; }
    foreach $term (@val2) { $sum_term_2 += $term; }

    return ( 2 * $num / ($sum_term_1 + $sum_term_2));

}

######################################################
##  DICE_SIM_B
##  This function assumes that the sum
##  of the term weights have been stored in advance for
##  each document and are passed as arguments.
##
######################################################

sub dice_sim_b {


    my $num  = shift;
    my $sum_term1 = shift;
    my $sum_term2 = shift;

    return ( 2 * $num / ($sum_term1 + $sum_term2) );
}
